buildscript {
    ext.kotlin_version = "1.4.30"
    repositories {
        mavenLocal()
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.31-alpha'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
}

subprojects {
    apply from: "$rootDir/common.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersion {
    doLast {
        if (!project.hasProperty("versionName")) {
            throw new IllegalStateException("The updateVersion task requires a versionName property to be passed as an argument")
        }
        def versionPropertiesFile = rootProject.file("version.properties")
        def existingProperties = new Properties()
        existingProperties.load(new FileInputStream(versionPropertiesFile))

        def versionName = project.getProperties().get("versionName")
        def versionCode = (existingProperties.versionCode as int) + 1

        if(versionName == existingProperties.versionName) {
            throw new IllegalStateException("The versionName '$versionName' is the current versionName")
        }

        versionPropertiesFile.write("versionName=$versionName\nversionCode=$versionCode")
    }
}