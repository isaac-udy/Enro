buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath deps.android.gradle
        classpath deps.kotlin.gradle
        classpath deps.hilt.gradle
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10'
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

subprojects {
    apply from: "$rootDir/common.gradle"
    apply from: "$rootDir/common_publish.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersion {
    doLast {
        if (!project.hasProperty("versionName")) {
            throw new IllegalStateException("The updateVersion task requires a versionName property to be passed as an argument")
        }
        def versionPropertiesFile = rootProject.file("version.properties")
        def existingProperties = new Properties()
        existingProperties.load(new FileInputStream(versionPropertiesFile))

        def versionName = project.getProperties().get("versionName")
        def versionCode = (existingProperties.versionCode as int) + 1

        if(versionName == existingProperties.versionName) {
            throw new IllegalStateException("The versionName '$versionName' is the current versionName")
        }

        versionPropertiesFile.write("versionName=$versionName\nversionCode=$versionCode")
    }
}

task disableConnectedDeviceAnimations {
    doLast {
        exec {
            commandLine(
                    "adb", "shell", "\"settings put global window_animation_scale 0.00\""
            )
        }

        exec {
            commandLine(
                    "adb", "shell", "\"settings put global transition_animation_scale  0.00\""
            )
        }
        exec {
            commandLine(
                    "adb", "shell", "\"settings put global animator_duration_scale 0.00\""
            )
        }
    }
}

task enableConnectedDeviceAnimations {
    doLast {
        exec {
            commandLine(
                    "adb", "shell", "\"settings put global window_animation_scale 1.00\""
            )
        }

        exec {
            commandLine(
                    "adb", "shell", "\"settings put global transition_animation_scale  1.00\""
            )
        }
        exec {
            commandLine(
                    "adb", "shell", "\"settings put global animator_duration_scale 1.00\""
            )
        }
    }
}