def versionProperties = new Properties()
versionProperties.load(new FileInputStream(rootProject.file("version.properties")))

ext.androidLibrary = {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-parcelize'

    android {
        compileSdkVersion 33

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 33
            versionCode versionProperties.getProperty("versionCode").toInteger()
            versionName versionProperties.getProperty("versionName")

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles "consumer-rules.pro"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_17.toString()
            freeCompilerArgs += "-Xjvm-default=enable"
        }

        buildFeatures {
            buildConfig = false
            viewBinding = true
        }
    }

    kotlin {
        explicitApi()
    }

    dependencies {
        implementation deps.kotlin.stdLib
    }
}

ext.useCompose = {
    android {
        buildFeatures {
            compose true
        }
        composeOptions {
            kotlinCompilerVersion "1.8.21"
            kotlinCompilerExtensionVersion "1.4.7"
        }
    }

    dependencies {
        implementation deps.compose.compiler
        implementation deps.compose.foundation
        implementation deps.compose.foundationLayout
        implementation deps.compose.ui
        implementation deps.compose.uiTooling
        implementation deps.compose.runtime
        implementation deps.compose.viewmodel
        implementation deps.compose.livedata
        implementation deps.compose.activity
        implementation deps.compose.material
    }
}
