plugins {
    id("com.google.devtools.ksp") version "1.8.21-1.0.11"
}
androidLibrary()
useCompose()
apply plugin: "kotlin-kapt"
apply plugin: "wtf.emulator.gradle"
publishAndroidModule("dev.enro", "enro")

android {
    namespace = "dev.enro"

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        animationsDisabled = true
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

emulatorwtf {
    if(project.hasProperty("ewApiToken")) {
        token = project.getProperties().get("ewApiToken").toString()
    } else {
        token = System.getenv().get("EW_API_TOKEN")
    }

    // devices to test on, Defaults to [[model: 'Pixel2', version: 27]]
    devices = [
            [model: 'NexusLowRes', version: 30, atd: true],
            [model: 'Pixel2', version: 27],
            [model: 'Pixel2', version: 23],
    ]

    numShards = 2
}

dependencies {
    releaseApi "dev.enro:enro-core:$versionName"
    debugApi project(":enro-core")

    releaseApi "dev.enro:enro-annotations:$versionName"
    debugApi project(":enro-annotations")

    lintPublish(project(":enro-lint"))

    kaptAndroidTest project(":enro-processor")

    testImplementation deps.testing.junit
    testImplementation deps.testing.androidx.junit
    testImplementation deps.testing.androidx.runner
    testImplementation deps.testing.robolectric
    testImplementation project(":enro-test")

    androidTestImplementation project(":enro-test")

    androidTestImplementation deps.testing.junit

    androidTestImplementation deps.kotlin.reflect
    androidTestImplementation deps.androidx.core
    androidTestImplementation deps.androidx.appcompat
    androidTestImplementation deps.androidx.fragment
    androidTestImplementation deps.androidx.activity
    androidTestImplementation deps.androidx.recyclerview

    androidTestImplementation deps.testing.androidx.fragment
    androidTestImplementation deps.testing.androidx.junit
    androidTestImplementation deps.testing.androidx.espresso
    androidTestImplementation deps.testing.androidx.espressoRecyclerView
    androidTestImplementation deps.testing.androidx.espressoIntents
    androidTestImplementation deps.testing.androidx.runner

    androidTestImplementation deps.testing.androidx.compose

    androidTestImplementation deps.androidx.navigation.fragment
    androidTestImplementation deps.androidx.navigation.ui

    androidTestImplementation deps.leakcanary
    androidTestImplementation deps.testing.leakcanary.instrumentation
}

afterEvaluate {
    tasks.findByName("preReleaseBuild")
            .dependsOn(
                    ":enro-core:publishToMavenLocal",
                    ":enro-annotations:publishToMavenLocal"
            )
}

